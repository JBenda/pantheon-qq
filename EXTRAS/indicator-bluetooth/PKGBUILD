# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=indicator-bluetooth
_actual_ver=0.0.6
_extra_ver=+14.04.20140207
pkgver=${_actual_ver}.daily14.02.07
pkgrel=102
pkgdesc="Bluetooth status indicator"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-bluetooth"
license=('GPL')
groups=('unity')
# Soft dependency
depends=('gnome-bluetooth')
makedepends=('gnome-common' 'intltool' 'vala')
# We don't want two bluetooth indicators
conflicts=('gnome-bluetooth-ubuntu')
install=indicator-bluetooth.install
# Ubuntu finally using xz sources? It's a miracle!
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-bluetooth_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001_bluetooth_browse_address_Removed.patch'
        '0002_Not_a_phone.patch')
sha512sums=('9c2843b07fdbb732d0630fccbd163594ccf1697d46c8c69a4d781d035528fe204d82446757c32f7650f0d0b2dcb14a33f359325a9935dcddabe6e752ff68f9e9'
            'c44735821c4468dcf249fae44d8fed936c2f60cb9f6545b12b476509750a95941d7697e5c288d0d06d92fcb84fb5031a3455cf910a263b725b15fddf416d1f58'
            '2b03d729930bcfc6aaeb8ca32226754e77089d1f791c1a2011f1ed487db4295675c8dcc8b924d9bb5426d4b16d34a9471d864e8b9ae8f85e544f8fc8074b4dc4')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  # bluetooth_browse_address() was removed in gnome-bluetooth 3.8, so don't link
  # against it
  #patch -p1 -i "${srcdir}/0001_bluetooth_browse_address_Removed.patch"
  patch -p1 -i "${srcdir}/0002_Not_a_phone.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  autoreconf -vfi
  intltoolize -f
  ./configure --prefix=/usr --libexecdir=/usr/lib/indicator-bluetooth --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install

  rm -r "${pkgdir}/usr/share/upstart/"

  # Use language packs
  rm -r "${pkgdir}/usr/share/locale/"
}

# vim:set ts=2 sw=2 et:
