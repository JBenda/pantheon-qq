# Maintainer: Que Quotion <quequotion@bugmenot.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libgala-git
pkgver=r1018.66b5d4a
pkgrel=1
pkgdesc='The Pantheon Window Manager (library and headers only)'
arch=('i686' 'x86_64')
url='https://github.com/elementary/gala'
license=('GPL3')
groups=('pantheon-unstable' 'pantheon-qq')
depends=('atk' 'bamf' 'cairo' 'gdk-pixbuf2' 'glib2' 'glibc' 'gnome-desktop'
         'gtk3' 'json-glib' 'libdrm' 'libgee' 'libgl' 'libx11' 'libxcomposite'
         'libxdamage' 'libxext' 'libxfixes' 'libxi' 'libxrandr' 'libxtst'
         'mesa' 'mutter' 'pango' 'plank' 'wayland'
         'libgranite.so')
makedepends=('git' 'gnome-common' 'granite-git' 'intltool' 'vala')
provides=('libgala.so')
conflicts=('libgala.so')
source=('git+https://github.com/elementary/gala.git')
sha256sums=('SKIP')

pkgver() {
  cd gala

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd gala

  ./autogen.sh \
    --prefix='/usr' \
    --disable-schemas-compile
  make
}

package() {
  cd gala

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/share/{applications,gala,glib-2.0,icons,locale}
  rm -rf "${pkgdir}"/usr/bin

}

# vim: ts=2 sw=2 et:
