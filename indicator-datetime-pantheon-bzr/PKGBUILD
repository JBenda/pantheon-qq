# Maintainer: Que Quotion <quequotion@bugmenot.com>
# Contributor: Michal Gawronski <dinth1906@gmail.com>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=indicator-datetime-pantheon-bzr
pkgbranch=0.1
pkgver=r3
pkgrel=1
epoch=1
pkgdesc="A very simple clock. (Canonical Ayatana Project)"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-datetime"
license=('GPL')
depends=('libindicate' 'libindicator' 'ido')
makedepends=('libindicator' 'bzr')
provides=('indicator-datetime')
conflicts=('indicator-datetime')
groups=('pantheon')
source=("bzr+lp:~elementary-os/elementaryos/os-patch-indicator-datetime-trusty"
	'0001-Get-timezone-from-systemd-s-timedate1-interface.patch'
        '0002_Not_a_phone.patch'
        'https://gist.githubusercontent.com/aabmass/2ca5568d409d4cf61b98/raw/47263c6104322f7c53900b933eefe7ca5ef35ee1/0003-Timezone-systemd-nullptr-check.patch')
sha512sums=('SKIP'
            '945caab8d7f46f182642bf24a9bca9ad7c1f0e2e6568d75c001daf97b71a3a013bf62815149503df3aedc63da64ab6e0259ee241b2c6205c2996b23669471e29'
            '74025891d27c5c70d0c92dc1e24a2c63bacade810f6997ee22e07d67f5804a023532fa339e583565ed08a4660c7dbed584382054625ca4eb217842fee1e41e33'
            '36c2bcaa55d57c838019b8b4486926ff55d5b5235922b02170473d12dfa1eee7bdf8216ae6dda1a4ec7bb93c320837afff64d0822b5eb2db1311ecd84f982e2a')

pkgver() {
  cd os-patch-indicator-datetime-trusty

  printf "r%s" "$(bzr revno)"
}

prepare() {
  cd "${srcdir}/os-patch-indicator-datetime-trusty"

  # Port to systemd's timedated (because /etc/timezone is no longer used)
  patch -p1 -i "${srcdir}/0001-Get-timezone-from-systemd-s-timedate1-interface.patch"
  patch -p1 -i "${srcdir}/0002_Not_a_phone.patch"
  patch -p1 -i "${srcdir}/0003-Timezone-systemd-nullptr-check.patch"
}

build() {

  cd "${srcdir}/os-patch-indicator-datetime-trusty" 
  export CFLAGS="${CFLAGS} -Wno-error"

  mkdir build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib \
    -Denable_tests=OFF

  make
}

package() {
  cd "${srcdir}/os-patch-indicator-datetime-trusty/build"

  make DESTDIR="${pkgdir}/" install

  rm -r "${pkgdir}/usr/share/upstart/"

  # Use language packs
  rm -r "${pkgdir}/usr/share/locale/"
}
