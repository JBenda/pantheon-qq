# Maintainer: Que Quotion <quequotion@bugmenot.com>
# Contributor: Eduardo Reveles <me@osiux.ws>
# Contributor: Shem Valentine <archlinux@valentinenews.net>
# Contributor: Lael Guillemenot <zeppelinlg@gmail.com>

pkgname=glippy
pkgver=0.6
pkgrel=6
pkgdesc="not so simple clipboard manager for GNOME"
arch=('i686' 'x86_64')
url="https://launchpad.net/glippy"
license=('GPL')
depends=('mono' 'gnome-sharp')
optdepends=('libappindicator-gtk2' indicator-application{,-gtk2})
makedepends=('automake' 'libappindicator-sharp')
options=(!emptydirs)
install=$pkgname.install
source=("http://hugsmile.eu/file/src/${pkgname}-${pkgver}.tar.gz"
        'enable-indicator.patch')
md5sums=('b1b74a867ab50245f4f360a50856a483'
         '60cb2f667f377c72b7923145d154a262')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}/"
  patch -p1 -i "${srcdir}/enable-indicator.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  #Using last autoconf
  PATH_AUTOCONF=$(find /usr/share -maxdepth 1 -type d -name "automake-*" | sort | tail -n 1)
  rm config.guess config.sub
  ln -s $PATH_AUTOCONF/config.guess .
  ln -s $PATH_AUTOCONF/config.sub .
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --enable-appindicator
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install

  install -m755 -d "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/$pkgname.schemas" \
    "$pkgdir"/etc/gconf/schemas/*.schemas
  rm -f "$pkgdir"/etc/gconf/schemas/*.schemas
}

# vim:set ts=2 sw=2 et:
