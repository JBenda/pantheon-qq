# Maintainer: Que Quotion <quequotion@bugmenot.com>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=bamf-bzr
pkgver=r604
pkgrel=1
epoch=1
pkgdesc="Removes the headache of applications matching into a simple DBus daemon and c wrapper library"
arch=('i686' 'x86_64')
url="https://launchpad.net/bamf"
license=('GPL')
depends=('libgtop' 'libwnck3' 'glib2' 'procps-ng')
makedepends=('gnome-common' 'gobject-introspection' 'gtk-doc' 'libxml2' 'libxslt' 'vala')
provides=('FEATURE-bamf-webapps' "libbamf3=${pkgver}" "bamfdaemon=${pkgver}" "bamf")
replaces=('libbamf3' 'bamfdaemon' 'bamf')
groups=('unity')
install=${pkgname}.install
source=("bzr+lp:bamf"
        'bamfwatcher.c'
        'update-bamf-index.pl'
        'bamfwatcher.service'
	'var-cache-index.patch')
sha512sums=('SKIP'
            'cdce36cd8b7b9deff849b305a940f1866e9fb75025365afb7976c2b1d1a23a0c68ff5b644fbfec0bd601e48e65748db76b223f483f4b9b9e7f772c7cb78d1977'
            '1ad920c19989a8e4b9acc3b84b4eaba1450ff67c649496fdf4485a327967b345a4869f399c8077e205a71ac4f4c0778d4c0a5b7adaf0d988fba7d3e6acaa9669'
            '844bb16542cdcb6d9d46fe791bb74f8362516668f9eae62a0a0cd10d86cf6ab2dd7df17af127c8a55e11e68fe0528a5daa94af465ac9e2c4a911542387f8234d'
            'cdcb8133a18b491bd767f3fc5d289710f1691b26b5dab3ec25c8e64b7bd722096e32001677327d7b0ca8098f9dffb29bf9c7aa57d48a05ad60c0835edc8069bf')

pkgver() {
  cd bamf

  printf "r%s" "$(bzr revno)"
}

prepare() {
  cd "${srcdir}/bamf"

  patch -Np2 < ../var-cache-index.patch

  sed -i 's/-Werror/-Wno-error/g' configure.ac

  #touch "${srcdir}/bamf-2.index"
}

build() {
  cd "${srcdir}"

  gcc bamfwatcher.c -lprocps -o bamfwatcher

  cd "bamf"

  gtkdocize
  autoreconf -vfi

  export PYTHON=/usr/bin/python2
  
  if ! pacman -Q xorg-server-xvfb &>/dev/null; then
    ./configure --prefix=/usr --libexecdir=/usr/lib --disable-static --enable-gtk-doc
  else
    ./configure --prefix=/usr --libexecdir=/usr/lib --disable-static --enable-gtk-doc --enable-headless-tests
  fi
  make
}

package() {
  cd "${srcdir}/bamf"

  make DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}/usr/bin/"
  install -dm755 "${pkgdir}/var/cache/bamf"
  touch "${pkgdir}/var/cache/bamf/bamf-2.index"
  chmod 755 "${pkgdir}/var/cache/bamf/bamf-2.index"
  install -m755 "${srcdir}/bamfwatcher" "${pkgdir}/usr/bin/"
  install -m755 "${srcdir}/update-bamf-index.pl" "${pkgdir}/usr/lib/bamf/"

  # Install systemd service
  install -dm755 "${pkgdir}/usr/lib/systemd/system/"
  install -m644 "${srcdir}/bamfwatcher.service" "${pkgdir}/usr/lib/systemd/system/"
}
