# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>
# Contributor: flamelab <panosfilip@gmail.com>

pkgname=wingpanel-standalone-bzr
pkgver=r154
pkgrel=1
epoch=1
pkgdesc='The Pantheon Panel (without Gala dependencies)'
arch=('i686' 'x86_64')
url='https://launchpad.net/wingpanel'
license=('GPL3')
groups=('pantheon-unstable')
depends=('clutter' 'cogl' 'glib2' 'glibc' 'gtk3' 'libgee' 'mutter'
         'libgranite.so')
makedepends=('bzr' 'cmake' 'vala')
provides=(wingpanel{,-bzr} 'libwingpanel-2.0.so')
conflicts=(wingpanel{,-bzr})
install='wingpanel.install'
source=('wingpanel::bzr+lp:wingpanel' 'minus-backgroundmanager.patch' 'minus-galaplugin.patch')
sha256sums=('SKIP'
            '4fbdbac4496743561d2100015d5af2aa81a4f7f87423fcd8a0124bd467a67326'
            '1f50f34a7d36fc8331c1080c42c38f8208e35f4551eed97705919d304d410c95')

pkgver() {
  cd wingpanel

  echo "r$(bzr revno)"
}

prepare() {
  cd wingpanel

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build

  #Standalone patches
  patch -Np2 < ../minus-backgroundmanager.patch
  patch -Np2 < ../minus-galaplugin.patch
}

build() {
  cd wingpanel/build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
    -DGSETTINGS_COMPILE='FALSE'
  make
}

package() {
  cd wingpanel/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
