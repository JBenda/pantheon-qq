# Maintainer: Que Quotion <quequotion@bugmenot.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=epiphany-pantheon
pkgver=3.22.1+5+g5d281a8
pkgrel=1
pkgdesc="A GNOME web browser based on the WebKit rendering engine"
url="https://wiki.gnome.org/Apps/Web"
arch=(i686 x86_64)
license=(GPL)
depends=(libsoup libnotify gsettings-desktop-schemas webkit2gtk nss iso-codes
         dconf gcr gnome-desktop libwnck3 gnome-themes-standard)
makedepends=(intltool itstool docbook-xml startup-notification
             gobject-introspection yelp-tools autoconf-archive appstream-glib git)
provides=('epiphany')
conflicts=('epiphany')
groups=(gnome)
_commit=5d281a891b956ad714ce91d37c64438eadeca7bc  # gnome-3-22

# Not interested
#        "99_urlbar_always_show.patch.repatch" - sacrifices title display
#        12_safetypes.patch - probably fixed by gnome team?
#        99_menu_remove_text_encoding.patch - multilingual users may need this
#        99_preferences_notebook_add_margin_border.patch - very complicated; aethetics only?
#        99_preferences_remove_fonts_style_tab.patch - rather have these prefences available
#        99_preferences_remove_text_encoding.patch - multilingual users may need this; not there anymore?
# Too broken
#        "99_tabs_double_click_opens_new_tab.patch.repatch" - causes segfaults on double-click tab-bar
#        "99_toolbar_add_bookmark_button.patch.repatch" - causes segfaults at runtime

source=("epiphany-pantheon::bzr+lp:/~elementary-os/elementaryos/os-patch-epiphany-browser-xenial"
        "81_ubuntu_force_update_bookmarks_menu.patch.repatch"
        "99_css_font_open_sans.patch.repatch"
        "99_css_rename_incognito_class.patch.repatch"
        "99_location_add_margin.patch.repatch"
        "99_menu_add_edit_bookmarks.patch.repatch"
        "99_menu_add_new_incognito_window.patch.repatch"
        "99_menu_add_preferences.patch.repatch"
        "99_overlay_style.patch.repatch"
        "99_tabs_close_button_left.patch.repatch"
        "99_tabs_close_on_middle_click.patch.repatch"
        "99_tabs_dont_expand.patch.repatch"
        "99_toolbar_24px_nonsymbolic_menu_icon.patch.repatch"
        "99_toolbar_add_home_button.patch.repatch"
        "99_toolbar_add_tooltips.patch.repatch"
        "99_urlbar_full_width.patch.repatch"
        "git://git.gnome.org/epiphany#commit=$_commit"
        "git://git.gnome.org/libgd"
        pluginsdir.diff)
sha256sums=('SKIP'
            'da706b556b3bafdf39d81e16dfbeaa041f253a0f1a6f6b76305106f77bbfe3f9'
            '986437fb2181c09e6b90788b24e5a76359d9b08c216b782df71a3d38f04b0064'
            '60ea4ad262b497521c5814e868923e7faf479dd1492d6ee60f6dbfe87053aa26'
            '96d954e9a6fc3fa436dc8a4cfec3aa4642c439a9b085a8a9070baaccae766ca5'
            '4de266659e94ca91e5fd0081279e297fa00b27a5ed5960c45c2f5d0b7e9e3460'
            'cf11815806cdbdf263016ac3904e1e5825c7f4858de7272e88ac49ac202b193c'
            'fdb6c71b231cebe2a305c077782e2219e00fb34c782e07119e57cf3871f3379f'
            'b8abf309959054402df664bbd82e076b18409ef7f07df0a22d86bb804776ad99'
            '1d555757ce30002b1bc8911d032e58c7e8ec524bd7b82519e6b873fa4a56f0cb'
            '3df68cfdc52202b5536785e68aee8dc1ed1513cf9e8ac1d67cf77ca1af4f3dc2'
            '4b9787837e11f2f769cc2eee7ae15116df2a29f3f2a8ecb8bcc421752667e776'
            'dfa961208e3de576a9aaa2a0e54a199999abc2d8adbcb8f7fd081fdfffe12462'
            '63346e10d30caf0ad86ca1a470cda62ee77abd105c26c8f554ce996652f6ef1e'
            '5c7fd03a562d12d6ea167f0a485ac10c1b51f1405607c086655a17637fb2df23'
            'f046d750315f16bdb48728bba15b8d04724ecd091a2881810cda3472229ab1d6'
            'SKIP'
            'SKIP'
            '42a7b820fd659fee8508a9b9a57e385c658b3b312c7a8f9456078223e4d78f22')

pkgver() {
  cd epiphany

  git submodule init
  git config --local submodule.libgd.url "$srcdir/libgd"
  git submodule update

  git describe --tags | sed 's/-/+/g'
}

prepare() {

  cd "$srcdir/epiphany-pantheon/debian/patches/"

  while read i; do
    if [[ "${i}" =~ ^# || -z "${i}" || "${i}" == *git* ]]; then
      continue # Skip comments, newlines, and git patches
    #Apply available repatches
    elif [ -f "../../../${i}.repatch" ]; then
        msg "Updating ${i} ..." 
        patch -Ns < "../../../${i}.repatch"
    fi
  done < series

  cd "$srcdir/epiphany"

  #Apply ElementaryOS patches
  while read i; do
    if [[ "${i}" =~ ^# || -z "${i}" || "${i}" == *git* ]]; then
      continue # Skip comments, newlines, and git patches
    elif [[ "${i}" == 12_safetypes.patch ]] || \
         [[ "${i}" == 99_menu_remove_text_encoding.patch ]] || \
         [[ "${i}" == 99_preferences_notebook_add_margin_border.patch ]] || \
         [[ "${i}" == 99_preferences_remove_fonts_style_tab.patch ]] || \
         [[ "${i}" == 99_preferences_remove_text_encoding.patch ]] || \
         [[ "${i}" == 99_toolbar_add_bookmark_button.patch ]] || \
         [[ "${i}" == 99_tabs_double_click_opens_new_tab.patch ]] || \
         [[ "${i}" == 99_urlbar_always_show.patch ]]; then
      continue # Skip reduntant or impossible to repair patches
    else
      msg "Applying ${i} ..."
      patch -Nsp1 < "${srcdir}/epiphany-pantheon/debian/patches/${i}"
    fi
  done < "${srcdir}/epiphany-pantheon/debian/patches/series"

  cd "$srcdir/epiphany"
  patch -Np1 -i ../pluginsdir.diff
  NOCONFIGURE=1 ./autogen.sh

}

build() {
  cd epiphany
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/epiphany --enable-debug

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd epiphany
  make DESTDIR="$pkgdir" install
}
