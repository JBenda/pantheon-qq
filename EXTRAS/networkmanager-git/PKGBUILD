# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
#
# (Added from network-manager package)
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinxu.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Will Rea <sillywilly@gmail.com>
# Contributor: Valentine Sinitsyn <e_val@inbox.ru>

_gitname=NetworkManager
pkgname=networkmanager-git
pkgver=0.9.10.beta1.416.gc6a1ca5
pkgrel=1
pkgdesc="Network Management daemon"
arch=(i686 x86_64)
url=http://www.gnome.org/projects/$_gitname
license=(GPL3)
depends=(dbus-glib dhclient libsoup polkit wpa_supplicant)
makedepends=(git gobject-introspection gtk-doc intltool libndp-git)
optdepends=('modemmanager: for modem management service'
    'dnsmasq: Connection sharing'
    'bluez: Bluetooth support'
    'openresolv: resolvconf support')
provides=("${pkgname%-*}=$pkgver" "libnm-glib=$pkgver")
conflicts=("${pkgname%-*}" 'libnm-glib')
backup=(etc/NetworkManager/NetworkManager.conf)
options=(!emptydirs !libtool)
install=$pkgname.install
source=($pkgname::git://anongit.freedesktop.org/$_gitname/$_gitname.git
    git://git.gnome.org/libgsystem
    NetworkManager.conf
    dnsmasq-path.patch)
sha512sums=('SKIP'
            'SKIP'
            '1f4e6c09da5a8921ee7759861cfa5e808c64cd27e733cd62cfb35776db05a226a73443c071730035480c9ba4abe85fa7daeb14aeffbbdae20ed193d78341f451'
            'df83427e3658947c3038138c0f0803c06cd85b9b7d89ebf464a9f454cc78498b30d6351511a2271cc6dd156567211ca2dda57f24fa72e913e2c000e797644ac4')

pkgver() {
    cd $pkgname/
    git describe --tags | sed 's/-/./g'
}

prepare() {
    cd $pkgname/
    find ../ -name '*.patch' -exec patch -Np1 -i '{}' \;
    rm -rf libgsystem/
    ln -sf ../libgsystem
}

build() {
    cd $pkgname/
    gtkdocize
    autopoint --force
    AUTOPOINT="intltoolize -f -c --automake" autoreconf -fi
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/usr/lib/networkmanager \
        --with-crypto=nss \
        --with-distro=arch \
        --with-dhclient=/usr/sbin/dhclient \
        --with-dhcpcd=/usr/sbin/dhcpcd \
        --with-iptables=/usr/sbin/iptables \
        --with-systemdsystemunitdir=/usr/lib/systemd/system \
        --with-udev-dir=/usr/lib/udev \
        --with-resolvconf=/usr/sbin/resolvconf \
        --with-session-tracking=systemd \
        --disable-static \
        --enable-more-warnings=no \
        --disable-wimax
    make
}

package() {
    make -C $pkgname DESTDIR="$pkgdir" install
    install -m644 $_gitname.conf "$pkgdir"/etc/$_gitname/
    rm -r "$pkgdir"/var/run
    # Provide native service for arch-daemons generator
    ln -s $_gitname.service "$pkgdir"/usr/lib/systemd/system/$pkgname.service
}
