# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: flamelab <panosfilip@gmail.com>

pkgbase=wingpanel-indicator-notifications
pkgname=wingpanel-indicator-notifications-bzr
pkgver=r46
pkgrel=1
pkgdesc='Notifications indicator for Wingpanel'
arch=('i686' 'x86_64')
url='https://launchpad.net/slingshot'
license=('GPL3')
groups=('pantheon')
depends=('libwnck3' 'granite-bzr' 'wingpanel-bzr')
makedepends=('bzr' 'cmake' 'vala')
provides=("${pkgbase}"{,-bzr}="$pkgver-$pkgrel")
conflicts=("${pkgbase}"{,-bzr})
install="${pkgbase}.install"
source=("${pkgbase}::bzr+lp:${pkgbase}")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgbase}"

  echo "r$(bzr revno)"
}

build() {
  cd "${pkgbase}"

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
    -DGSETTINGS_COMPILE='FALSE' \
    -DUSE_UNITY='FALSE'
  make -j1
}

package() {
  cd "${pkgbase}"/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
