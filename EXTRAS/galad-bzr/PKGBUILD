# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=galad-bzr
pkgver=r488
pkgrel=1
pkgdesc='The Pantheon Window Manager (background daemons only)'
arch=('i686' 'x86_64')
url='https://launchpad.net/gala'
license=('GPL3')
groups=('pantheon-unstable')
depends=('clutter-gtk' 'mutter' 'plank'
         'libgranite.so')
makedepends=('bzr' 'gnome-common' 'granite-bzr' 'intltool' 'vala')
#provides=('gala' 'libgala.so')
provides=('galad')
#conflicts=('gala')
install='gala.install'
source=('bzr+lp:gala'
        'galad-bzr.patch'
        'galad-bzr2.patch')
sha256sums=('SKIP'
            '5a4b73e73844871cf652135e1114aec0bee79768ab08a9982f8495c4506875fe'
            'ebedcd47eb797fbf6d2974ac6ade051285de71408ad3321ffb15c7b4c8637fda')

pkgver() {
  cd gala

  echo "r$(bzr revno)"
}

prepare() {
  cd gala
  patch -Np2 < ../galad-bzr.patch
  patch -Np2 < ../galad-bzr2.patch
}

build() {
  cd gala

  ./autogen.sh \
    --prefix='/usr' \
    --disable-schemas-compile
  make
}

package() {
  cd gala

  make DESTDIR="${pkgdir}" install
  rm "${pkgdir}"/usr/share/glib-2.0/schemas/org.pantheon.desktop.gala.gschema.xml
}

# vim: ts=2 sw=2 et:
