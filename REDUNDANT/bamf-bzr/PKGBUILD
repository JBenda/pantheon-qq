# Maintainer: Que Quotion <quequotion@bugmenot.com>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=bamf-bzr
pkgver=r601
pkgrel=1
epoch=1
pkgdesc="Removes the headache of applications matching into a simple DBus daemon and c wrapper library"
arch=('i686' 'x86_64')
url="https://launchpad.net/bamf"
license=('GPL')
depends=('libgtop' 'libwnck3' 'glib2' 'procps-ng')
makedepends=('gnome-common' 'gobject-introspection' 'gtk-doc' 'libxml2' 'libxslt' 'vala')
provides=('FEATURE-bamf-webapps' "libbamf3=${pkgver}" "bamfdaemon=${pkgver}" "bamf")
replaces=('libbamf3' 'bamfdaemon' 'bamf')
groups=('unity')
install=${pkgname}.install
source=("bzr+lp:bamf"
        'bamfwatcher.c'
        'update-bamf-index.pl'
        'bamfwatcher.service')
sha512sums=('SKIP'
            '054ef0f0e9bdd4ccf5dfdab95bde0a17cd2f1249473879453a41919feb8f6d88f1bc71d2ff34f68cae728825cdb2df4663f0bdfa8e3cc4012c474dc81e29492f'
            '7db84774b72fb330abe2116f3d1d842c007820c200344d76ff87abab5226488fcc21d2e82cbe5381fb3e7ce0eacc09232f17d64c4e2a5ee8eef0be06808f21ff'
            '844bb16542cdcb6d9d46fe791bb74f8362516668f9eae62a0a0cd10d86cf6ab2dd7df17af127c8a55e11e68fe0528a5daa94af465ac9e2c4a911542387f8234d')

pkgver() {
  cd bamf

  printf "r%s" "$(bzr revno)"
}

prepare() {
  cd "${srcdir}/bamf"

  sed -i 's/-Werror/-Wno-error/g' configure.ac
}

build() {
  cd "${srcdir}"

  gcc bamfwatcher.c -lprocps -o bamfwatcher

  cd "bamf"

  gtkdocize
  autoreconf -vfi

  export PYTHON=/usr/bin/python2
  
  if ! pacman -Q xorg-server-xvfb &>/dev/null; then
    ./configure --prefix=/usr --libexecdir=/usr/lib --disable-static --enable-gtk-doc
  else
    ./configure --prefix=/usr --libexecdir=/usr/lib --disable-static --enable-gtk-doc --enable-headless-tests
  fi
  make
}

package() {
  cd "${srcdir}/bamf"

  make DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}/usr/bin/"
  install -dm755 "${pkgdir}/var/cache/bamf/"
  install -m755 "${srcdir}/bamfwatcher" "${pkgdir}/usr/bin/"
  install -m755 "${srcdir}/update-bamf-index.pl" "${pkgdir}/usr/lib/bamf/"

  # Install systemd service
  install -dm755 "${pkgdir}/usr/lib/systemd/system/"
  install -m644 "${srcdir}/bamfwatcher.service" "${pkgdir}/usr/lib/systemd/system/"
}
